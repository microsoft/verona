if(NOT DEFINED CMAKE_SYSTEM_NAME
    OR NOT DEFINED LLVM_BUILD_TYPE
    OR NOT DEFINED LLVM_PACKAGE_GIT_VERSION
    OR NOT DEFINED OUTPUT_DIR)
  message(FATAL_ERROR "Variables CMAKE_SYSTEM_NAME, LLVM_BUILD_TYPE, LLVM_PACKAGE_GIT_VERSION and OUTPUT_DIR must be provided.")
endif()

string(TOLOWER ${LLVM_BUILD_TYPE} BUILD_TYPE)
string(TOLOWER ${CMAKE_SYSTEM_NAME} PLATFORM)

set(LLVM_URL https://verona.blob.core.windows.net/llvmbuild)
set(PKG_NAME verona-llvm-install-x86_64-${PLATFORM}-${BUILD_TYPE}-${LLVM_PACKAGE_GIT_VERSION})
set(MD5_NAME ${PKG_NAME}.md5)

message(STATUS "Downloading LLVM checksum at ${LLVM_URL}/${MD5_NAME}")

file(DOWNLOAD "${LLVM_URL}/${MD5_NAME}" ${OUTPUT_DIR}/${MD5_NAME} STATUS MD5_DOWNLOAD_STATUS)
list(GET MD5_DOWNLOAD_STATUS 0 MD5_DOWNLOAD_STATUS_CODE)
if (NOT (${MD5_DOWNLOAD_STATUS_CODE} EQUAL 0))
  list(GET MD5_DOWNLOAD_STATUS 1 ERROR_MESSAGE)
  message(FATAL_ERROR "Failed to download md5 hash: ${ERROR_MESSAGE}")
endif ()
file(STRINGS ${OUTPUT_DIR}/${MD5_NAME} LLVM_MD5_SUM REGEX [0-9a-f]+)
string(STRIP ${LLVM_MD5_SUM} LLVM_MD5_SUM)

message(STATUS "Downloading LLVM at ${LLVM_URL}/${PKG_NAME}")

if (VERBOSE_LLVM_DOWNLOAD)
  file(DOWNLOAD
    "${LLVM_URL}/${PKG_NAME}"
    "${OUTPUT_DIR}/${PKG_NAME}.tar"
    SHOW_PROGRESS
    EXPECTED_HASH MD5=${LLVM_MD5_SUM})
else()
  file(DOWNLOAD
    "${LLVM_URL}/${PKG_NAME}"
    "${OUTPUT_DIR}/${PKG_NAME}.tar"
    EXPECTED_HASH MD5=${LLVM_MD5_SUM})
endif()

message(STATUS "Extracting LLVM")

file(ARCHIVE_EXTRACT
  INPUT "${OUTPUT_DIR}/${PKG_NAME}.tar"
  DESTINATION ${OUTPUT_DIR}/${PKG_NAME})

if (EXISTS ${OUTPUT_DIR}/install)
  file(REMOVE_RECURSE ${OUTPUT_DIR}/install)
endif ()
file(RENAME ${OUTPUT_DIR}/${PKG_NAME}/build ${OUTPUT_DIR}/install)
file(REMOVE ${OUTPUT_DIR}/${PKG_NAME})

file(TOUCH ${OUTPUT_DIR}/llvm-stamp)

message(STATUS "Done.")
